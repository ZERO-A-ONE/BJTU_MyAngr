## 8086汇编笔记06

## int指令

int中断是一种重要的内中断，它的功能是引发中断过程。可以在程序中使用int指令调用任何一个中断的中断处理程序。可见int指令的最终功能和call指令相似，都是调用一段程序

因此一般情况下，系统将一些具有一定功能的子程序，以中断处理程序的方式提供给应用程序调用

我们在编程的时候，可以用int指令调用这些子程序。当然，也可以自己编写一些中断处理程序供别人使用。可以将中断处理程序简称为中断例程

```assembly
int n ;n为中断类型码
```

执行过程如下：

- 取中断类型码n
- 标志寄存器入栈，IF=0，TF=0
- CS、IP入栈
- (IP)=(n*4)，(CS)=(n\*4+2)
- 从此转去执行n号中断的中断处理程序

#### 例子一：

安装程序

```assembly
;编程：安装中断7ch的中断例程
;功能：求一个word型数据的平方
;参数：（ax）= 要计算的数据
;返回值：dx、ax中存放结果的高16位和低16位
assume cs:code
code segment
start:
        mov ax,cs 
        mov ds,ax
        mov si,offset sqr   ;设置ds:si指向源地址
        mov ax,0
        mov es,ax
        mov di,200h         ;设置es:di指向目的地址
        mov cx,offset sqrend - offset sqr   ;设置cx为传输长度
        cld                 ;设置传输方向为正
        rep movsb

        mov ax,0
        mov es,ax
        mov word ptr es:[7ch*4],200h
        mov word ptr es:[7ch*4+2],0

        mov ax,4c00h
        int 21h
sqr:
        mul ax
        inret 				;pop IP pop CS popf
sqrend:
        nop
code ends
end start
;编程实现求平方功能的程序
;安装程序，我们将其安装在0:200处
;设置中断向量表，将程序的入口地址保存在7ch表项中，使其成为中断7ch的中断例程
```

调用测试程序

```assembly
assume cs:code
code segment
start:
        mov ax,3456
        int 7ch         ;调用中断7ch的中断例程，计算ax中的数据的平方
        add ax,ax
        adc dx,dx       ;存放结果，将结果乘以2
        mov ax,4c00h
        int 21h
code ends
end start
```

#### 例子二：

安装程序：

```assembly
assume cs:code
code segment
start:
        mov ax,cs
        mov ds,ax
        mov si,offset capital
        mov ax,0
        mov es,ax
        mov di,200h
        mov cx,offset capitalend - offset capital
        cld
        rep movsb

        mov ax,0
        mov es,ax
        mov word ptr es:[7ch*4],200h
        mov word ptr es:[7ch*4+2],0

        mov ax,4c00h
        int 21h
capital:
        push cx
        push si
change:
        mov cl,[si]
        mov ch,0
        jcxz ok
        and byte ptr [si],11011111b
        inc si
        jmp short change
ok:
        pop si
        pop cx
        iret 
capitalend:nop
code ends
end start
```

测试程序：

```assembly
assume cs:code
data segment
    db 'conversation',0
data ends
code segment
start:
        mov ax,data 
        mov ds,ax
        mov si,0
        int 7ch  
        mov ax,4c00h
        int 21h
code ends
end start
```

这里主要是在中断例程capital中用到了寄存器si和cx，编写中断例程和编写子程序的时候具有同样的问题，就是要避免寄存器的冲突

总而言之就是要注意用到的寄存器的值的保存和恢复

## 使用bp代替sp

例如：

```assembly
lp:
	push bp
	mov bp,sp
	dec cx
	jcxz lpret
	add [bp+2],bx
lpret:
	pop bp
	iret
```

用bp寄存器保存调用前栈的sp，用bp代替sp，保证栈不容易被破坏，在结束函数调用后，弹出bp恢复sp

## BIOS和DOS中断例程的安装过程

之前我们都是自己编写中断例程，将它们放在安装程序中，然后通过运行安装程序，将他们安装到指定的内存区中，此后，别的应用程序才可以调用，那BIOS和DOS提供的中断例程是如何安装到内存中的呢？

- 开机后，CPU一加电，初始化(CS)=0FFFFH，(IP)=0，自动从FFFF:0单元开始执行程序。在FFFF:0处有一条跳转指令，CPU执行该指令后，转去执行BIOS中的硬件系统检测和初始化程序
- 初始化程序将建立BIOS所支持的中断向量，即将BIOS提供的中断例程的入口地址登记在中断向量表中
- 硬件系统检测和初始化完成后，调用int 19h经行操作系统的引导，从此将计算机交由操作系统控制
- DOS启动后，除完成其他工作外，还将它所提供的中断例程装入内存，并建立相应的中断向量

## BIOS中断例程应用

一般来说，一个供程序员调用的中断例程中往往包括多个子程序，中断例程内部用传递进来的参数来决定执行哪个子程序。BIOS和DOS提供的中断例程，都用ah来传递内部子程序的编号

例如int 10h中断例程是BIOS提供的中断例程，其中包含了多个和屏幕输出相关的子程序

#### 设置光标位置

利用int 10h中断例程设置光标的位置功能：

```assembly
mov ah,2	;表示调用第10h号中断例程的2号子程序
mov bh,0	;设置光标到第0页
mov dh,5	;第5行
mov dl,12	;第12列
int 10h
```

(ah)=2表示调用第10h号中断例程的2号子程序，功能为设置光标位置，可以提供光标所在的行号（80\*25字符模式下：0~24）、列号（80\*25字符模式下：0~79），和页号作为参数

- bh中页号的含义：在内存地址空间中，B8000h~BFFFFh共32K的空间，为80*25彩色字符模式的显示缓冲区
- 一屏的内容在现实缓冲区中共占4000个字节
- 也就是说，通常情况下，B8000h~BFF9Fh中的4000个字节的内容将出现在显示器上

#### 显示字符

```assembly
mov ah,9	;放置光标
mov al,'a'	;字符
mov bl,7	;颜色属性
mov bh,0	;第0页
mov cx,3	;字符重复个数
int 10h
```

bl中的颜色属性格式如下：

|  7   |  6   |  5   |  4   |  3   |  2   |  1   |  0   |
| :--: | :--: | :--: | :--: | :--: | :--: | :--: | :--: |
|  BL  |  R   |  G   |  B   |  I   |  R   |  G   |  B   |
| 闪烁 | 背景 | 背景 | 背景 | 高亮 | 前景 | 前景 | 前景 |

例如：

```assembly
;在屏幕的5行12列显示3个红底高亮闪烁绿色的'a'
assume cs:code
code segment
strat:
        mov ah,2	;表示调用第10h号中断例程的2号子程序
        mov bh,0	;设置页
        mov dh,5	;ah中放行号
        mov dl,12	;dl中放列号
        int 10h

        mov ah,9	;放置光标
        mov al,'a'	;字符
        mov bl,11001010b	;颜色属性
        mov bh,0	;第0页
        mov cx,3	;字符重复个数
        int 10h

        mov ax,4c00h
        int 21h
code ends
end start
```

## DOS中断例程应用

int 21h中断例程是DOS提供的中断例程，其中包括了DOS提供给程序员在编程时调用的子程序

#### 程序返回

我们从前一直使用的是int 21中断例程的4ch号功能，即程序返回功能，可以提供返回值作为参数：

```assembly
mov ah,4ch
mov al,0
int 21h
```

我们一般合并写成：

```assembly
mov ax,4c00h
int 21h
```

#### 光标位置显示字符串

(ah)=9表示调用第21h号中断例程的9号子程序，功能为在光标位置显示字符串，可以提供要显示字符串的地址作为参数

```assembly
ds:ds指向字符串	;要显示的字符串需用"$"作为结束符
mov ah,9	   ;功能号9，表示在光标位置显示字符串
int 21h
```