# 80x86汇编笔记03

## 标号、变量和数据结构

当程序中要跳转到另一位置时，需要有一个标识来指示位置，这就是标号，通过在目的地址的前面放一个标号，可以在指令中使用标号来代替直接使用地址

变量的值在程序运行中是经常改变的，所以它必须定义在可写的段内，如data和data?，或者在堆栈内。按照定义的位置不同，MASM中的变量也分为全局变量和局部变量

在MASM中标号和变量的命名规范是相同的：

- 可以用字母、数字、下划线以及符号@、$和？
- 第一个符号不能是数字
- 长度不能超过240个字符
- 不能使用指令名等关键字
- 在作用域内必须是唯一的

## 标号的定义

格式一：

```assembly
标号名： 目的指令
```

格式二：

```assembly
标号名:: 目的指令
```

标号的作用域是当前的子程序，在不同子程序中可以存在同样名字的标号，这也就意味着这种格式不能从一个子程序通过标号跳转到另一个子程序中。如果是在需要从一个子程序中用指令跳转到另一个子程序中的标号位置时候，我们用格式二

## @@

因为标号有限，我们一般用下面的方式来取标号，节省标号开支

```assembly
mov	cx,1234h
cmp flag,1
je	@F	；F这里是Forward的意思
mov	cx,1000h
@@:
	lopp @B ;B这里是Before的意思
```

当用@@做标号时，@F表示本条指令后的第一个@@标号，@B表示本条指令前的第一个@@标号

> 当程序中可以有多个@@标号，@B和@F只寻找匹配最近的一个

不要在间隔太远的代码中使用@@符号，距离最好限制在编辑器能够显示的同一屏幕的范围内

## 变量

### 全局变量

全局变量的作用域是整个程序，Win32汇编的全局变量定义在data或data？段内，可以同时定义变量的类型和长度，格式如：

```
变量名	类型	初始值1，初始值2，……
变量名	类型	初重复数量 dup(始值1，初始值2，……)
```

|          名称          | 表示方式 | 缩写 | 长度（字节） |
| :--------------------: | :------: | :--: | :----------: |
|          字节          |   byte   |  db  |      1       |
|           字           |   word   |  dw  |      2       |
|   双字(double word)    |  dword   |  dd  |      4       |
|     三字(far word)     |  fword   |  df  |      6       |
|    四字(quad word)     |  qword   |  dq  |      8       |
| 十字节BCD码(ten byte)  |  tbyte   |  dt  |      10      |
| 有符号字节(sign byte)  |  sbyte   |      |      1       |
|  有符号子(sign byte)   |  sword   |      |      2       |
| 有符号双字(sign dword) |  sdword  |      |      4       |
|      单精度浮点数      |  real4   |      |      4       |
|      双精度浮点数      |  real8   |      |      8       |
|      10字节浮点数      |  real10  |      |      10      |

> 所有使用到变量类型的情况中，只有定义全局变量的时候才可以用缩写

在byte类型变量的定义中，可以用引号定义字符串和数值定义的方法混用

假设要定义两个字符串Hello,World!和Hello again，每个字符串后面中回车和换行符，最后以一个0字符结尾，可以定义如下：

```assembly
szText	db	"Hello World!",0dh,0ah,\
"Hello again",0dh,0ah,0
```

DOS和Windows采用回车+换行(CR/LF)表示下一行，而UNIX/Linux采用换行符(LF)表示下一行，macOS则采用回车符(CR)表示下一行

- CR用符号"\\r"表示，十进制ASCII代码是13，十六进制代码为0x0D
- LF使用”\n“符号表示，ASCII代码是10，十六进制为0x0A

全局变量在定义中既可以指定初值，也可以预留，在data？段中，只能用问号预留空间，在data？段中不能指定初始值

### 局部变量

局部变量这个概念出现以后，两个以上子程序都要用到的数据才被定义为全局变量统一放在数据段中，仅在子程序内部使用的变量则放在堆栈中，这样还可以使得子程序编程黑匣子的模样，使程序的模块结构更加分明

局部变量的作用域是单个子程序，在进入子程序的时候，通过修改堆栈指针esp来预留出需要的空间，再用ret指令返回主程序之前，同样通过恢复esp丢弃这些空间

局部变量因为空间是临时分配的，所以无法定义含有初始化值的变量，对局部变量的初始化一般在子程序中由指令完成

#### 局部变量的定义

MASM用local伪指令提供了对局部变量的支持，格式是：

```assembly
local 变量名1[[重复数量]][:类型]，变量名2[[重复数量]][:类型]……
```

local伪指令必须紧接在子程序定义的伪指令proc后，其它指令开始前，这是因为局部变量的数目必须在子程序开始的时候就确定下来系统才知道如何分配，在一个local语句定义不下的时候，可以有多个local语句，**语法中的数据类型不能用缩写**

- Win32汇编默认的类型是dword，如果定义dword类型的局部变量，则类型可以省略
- 当定义数组的时候，可以[]括号起来。**不能使用定义全局变量的dup伪指令**
- 局部变量不能和已定义的全局变量同名
- 局部变量的作用域是当前子程序，所以在不同的子程序中可以有同名的局部变量

一些例子：

```assembly
local	loc1[1024]:byte	;定义了一个1024字节长的局部变量loc1
local	loc2	;定义了一个名为loc2的局部变量，类型是默认值dword
local	loc3：WNDCLASS	;定义了一个WNDCLASS数据结构，名为loc3
```

#### 局部变量的使用

```assembly
TestProc proc	;名为TestProc的子程序
local	@loc1:dword,@loc2:word
local	@loc3:byte	;用local语句定义了3个变量
mov	eax,@loc1	;对应类型进行转储，然后返回
mov	ax,@loc2
mov	al,@loc3
ret
TestProc	endp
```

其实在编译后的文件再反汇编的话，可以发现编译器编译后的指令比源程序多了前后两段指令：

```assembly
push ebp
mov	ebp,esp
add	esp,FFFFFFF8
……
leave
```

这就是使用局部变量所必须的指令，分别用于局部变量的准备工作和扫尾工作

##### 分析过程：

- 当调用者执行了call TestProc指令后，CPU把返回的地址（当前地址）压入堆栈，再转移（jmp）到子程序执行
- esp在程序的执行过程中可能随时用到，不可能用esp来随时存取局部变量，ebp寄存器是以堆栈段为默认数据段的，所以，可以用ebp做指针指向堆栈代替esp
- 于是，在初始化前，先用一句push ebp指令把原来的ebp保存起来，然后把esp的值传递给ebp

因为在80386处理器中，以dword(32位)为界对齐时读取内存速度最快，所以MASM宁可浪费一些内存单元进行对齐

在程序退出前，必须把正确的esp设置回去，否则，ret指令会从堆栈中取出错误的地址返回，因为有`mov ebp,esp`，所以要返回的时候只要先`mov esp,ebp`，然后再`pop ebp`，堆栈就是正确的了

在80386指令集中有一条指令可以在一句中实现这些功能，就是leave指令，所以，编译器在ret指令之前只使用了一句leave指令

和全局变量不一样，局部变量的初始值是随机的，是其他子程序执行后在堆栈里留下的垃圾（因为我们知道，腾出空间只是改变栈指针esp），所以，对局部变量的值一定要初始化，特别是定义为结构后当参数传递给API函数的时候

## 变量的使用

在MASM中以不同的类型访问不会对变量造成影响。在MASM中，如果要用指定类型之外的长度访问变量，必须显式地指出要访问地长度，这样编译器忽略语法上的长度检验，仅使用变量地地址

```
类型	ptr	变量名
```

类型可以是byte，word，dword，fword，qword，real8和real10，如：

```assembly
mov	ax,word	ptr	szBuffer
```

> 指定类型地参数访问并不会去检测长度是否溢出

例子：

```assembly
.data
bTest1 db 12h
wTest2 dw 1234h
dwTest3 dd 12345678h
……
.code
mov	al,bTest1
mov	ax,word ptr bTest1
mov eax,dowrd ptr bTest1
```

`mov al,bTest1`这一句显然使al等于12h，下面的两句，ax和eax难道等于0012h和00000012h吗？实际运行的结果使3412h和78123412h

下面是反汇编后的结果：

```assembly
;.data段中的变量
;.data段中变量是按顺序从低地址往高地址排列的，对于超过一个字节的数据，80386处理器的数据排列方式是小端序
00403000 12 34 12 78 56 34 12
;.code段中的代码
00401000	A000304000	mov	al,byte	ptr [00403000]
00401005	66A10030400	mov	ax,word	ptr [00403000]
0040100B	A100304000	mov eax,dword	ptr [00403000]
```

在执行指令`mov ax,word ptr bTest1`的时候，是从bTest1的地址403000h处取一个字，其长度已经超过了bTest1的范围并落到了wTest2中。从内存中看，是取了bTest1的数据12h和wTest2的低位数据34h，在这两个字节中，12h位于低地址，所以ax中的数值是3412h

同理，看`mov eax,dword ptr bTest1`这条指令取了bTest1，wTest2的全部和dwTest3的最低位78h，在内存中的排列是12h 34h 12h 78h，所以eax等于78123412h

> 这个例子说明了汇编用ptr强制覆盖变量长度的时候，实质上是只用了变量的地址而禁止编译器进行检验。编译器并不会考虑定界的问题，程序员在使用的时候必须对内存中的数据排列有个全局概念，以免越界存取到意料之外的数据

如果只是想把bTest1的一个字节拓展到一个字或一个双字再放到ax或eax中，高位保持0而不是越界存取到其他的变量，80386处理器提供的movzx指令可以实现这个功能，例如：

```assembly
movzx	ax,bTest1	;ax == 0012h
movzx	eax,bTest1	;eax == 00000012h
movzx	eax,cl		;eax == 000000(cl)
movzx	eax,ax		;eax == 0000(ax)
```

> 用movzx指令进行数据长度扩展是Win32汇编中常用技巧

movsx是有符号的数据扩展

## 变量的尺寸和数量

在源程序中用到变量的尺寸和数量的时候，可以用sizeof和lengthof伪指令来实现，格式是：

```
sizeof	变量名、数据类型或数据结构名
lengthof	变量名
```

它们的区别是：

- sizeof伪指令可以取得变量、数据类型或数据结构以字节为单位的长度
- lengthof则可以取得变量中数据的项数

例如：

```assembly
stWndClass	WNDCALSS <>
szHello	db	'Hello,world',0
dwTest	dd	1,2,3,4
...
.code
...
mov	eax,sizeof stWndClass
mov ebx,sizeof	WNDCLASS
mov ecx,sizeof	szHello
mov edx,sizeof	doword
mov esi,sizeof	dwTest
```

- 执行后eax的值是stWndClass结构的长度：40
- ebx同样是：40
- ecx的值是szHello的长度：13
- edx的值是一个双字的长度：4

如果把所有的sizeof换成lengthof：

- 那么eax会等于1，因为只定义了1项WNDCLASS
- ecx同样等于13
- esi则等于4
- lengthof WNDCLASST和lengthof dword是非法的用法，编译器报错

如果分两行定义：

```assembly
szHello	db	'hello',0dh,0ah
		db	'world',0
```

sizeof szHello的值是7而不是13.因为MASM中的变量定义只认一行，后一行实际上是另一个没有名称的数据定义

## 获取变量地址

获取变量地址的操作对于全局变量和局部变量是不同的

### 全局变量

对于全局变量，它的地址在编译的时候已经由编译器确定了：

```
mov	寄存器，offset 变量名
```

其中offset是取变量地址的伪操作符

### 局部变量

对于局部变量，它是用ebp来做指针操作的，假设ebp的值是40100h，那么第一个局部变量的地址是ebp-4即400FCh

由于ebp的值随着程序的执行环境不同可能是不同的，所以局部变量的地址值在编译的时候也是不确定的，不能用offset伪操作符来获取它的地址

### 取指针的地址

80386处理器中有一条指令用来获取指针的地址，就是lea指令，如：

```
lea	eax,[ebp-4]
```

该指令可以在运行时按照ebp的值实际计算出地址放到eax中

如果要在invoke伪指令的参数中用到一个局部变量的地址，MASM对此有一个专用的伪操作符addr，其格式为：

```
addr 局部变量名 或 全局变量名
```

当addr后跟全局变量名的时候，用法和offset是相同的

当addr后面跟局部变量名的时候，编译器自动用lea指令先把地址取到eax中，然后用eax来代替变量地址

> addr伪操作符只能在invoke的参数中使用，不能用在类似的场合：
>
> `move ax,addr 局部变量名`

假设在一个子程序中有如下invoke指令：

```assembly
invoke	Test,eax,addr szHello
```

编译器会把invoke伪指令和addr翻译成下面这样：

```assembly
lea eax,[ebp-4]
push eax	;参数2：addr szHello
push eax	;参数1：eax
call Test
```

编译器会报错，也就是说，要用到的eax的值不再有效，所以挡在invoke中使用addr伪操作符时，注意在它的前面不能用eax，否则eax的值会被覆盖掉，当然eax在addr的后面的参数中用是可以的